"""
    Use py_wheel_dist for each target to create a dist/* folder to upload to pypi.
    python -m twine upload dist/*
"""

load("@pybind11_bazel//:build_defs.bzl", "pybind_extension", "pybind_library")
load("@rules_python//python:defs.bzl", "py_library")
load("@rules_python//python:packaging.bzl", "py_package", "py_wheel", "py_wheel_dist")

pybind_library(
    name = "odrive_driver_cc_library",
    srcs = ["odrive_driver_py.cc"],
    deps = [
        "//odrive-api:containers",
        "//odrive-api/interface:odrive_driver",
        "//odrive-api/logger:logger",
        "//odrive-api/communication:odrive_socket",
    ],
    alwayslink = True,
    visibility = ["//visibility:public"],
)

pybind_extension(
    name = "odrive_driver",
    deps = [":odrive_driver_cc_library"],
    visibility = ["//visibility:public"],
)

py_library(
    name = "odrive_driver_library",
    data = [":odrive_driver"],
    imports = ["."],
    visibility = ["//visibility:public"],
)

py_package(
    name = "odrive_driver_pkg",
    deps = [":odrive_driver_library"],
)

exports_files(
    ["description.md"],
    visibility = ["//visibility:public"],
)

py_wheel(
    name = "odrive_api",
    twine = "@publish_deps//twine",
    distribution = "odrive_api",
    summary = "Custom python bindings for the Odrive motor controllers.",
    description_file = ":description.md",
    python_requires = ">=3.12",
    python_tag = "py3",
    version = "0.0.3",
    deps = [
        ":odrive_driver_pkg",
    ],
    platform = select({
        "@platforms//cpu:x86_64": "manylinux2014_x86_64",
        "@platforms//cpu:aarch64": "manylinux2014_aarch64",
    }),
)

py_wheel_dist(
    name = "odrive_api_dist",
    out = "dist",
    wheel = ":odrive_api",
    visibility = ["//visibility:public"],
)
